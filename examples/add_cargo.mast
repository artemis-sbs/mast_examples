# import is not tracking the basedir, I think it should
import examples\bar.mast
logger 

raider_count = 4

if IS_SERVER:
    ->server_main
else:
    -> client_main
end_if

============== server_main ========
started = False
=> map

============== server_page =========
section style="area: 20,40, 80,60;"

""" This is the example script"""

await choice:
    + "Start Mission" if started==False:
    ~~ sbs.resume_sim()~~
    + "Resume Mission" if started==True:
    ~~ sbs.resume_sim() ~~
end_await
->server_page


========= map ================
do sbs.create_new_sim()
delay gui  1s
do create_map(mast_scheduler)

->END

========= spawn_wave ===========
# Watches for no more raiders
# if there are not any, spawn more
if len(role("raider")) == 0:
    do create_wave(mast_scheduler, raider_count = 4)
    raider_count = raider_count + 1
end_if
delay 10s
-> spawn_wave

========= client_main ==========
event change_console:
    ->select_console
end_event
console = "helm"
ship = "artemis"

========== select_console ==========

section style="area: 60,50, 75,90;"
vradio ship "artemis, hera, atlas"

section style="area: 85,50, 99,90;"
vradio console "helm,weapons, comms,science,engineering"
blank
row
button "accept":
    ->console_selected
end_button

await gui

->END

====== console_selected ====

id = 0
match ship:
    case "artemis":
        id = artemis.id
    case "hera":
        id = hera.id
    case "atlas":
        id = atlas.id
end_match

~~
sbs.assign_client_to_ship(client_id, id)
~~

match console:
    case "helm":
        widget_list "normal_helm" "3dview^2dview^helm_movement^throttle^request_dock^shield_control^ship_data^text_waterfall^main_screen_control"
    case "weapons":
        widget_list "normal_weap" "2dview^weapon_control^ship_data^shield_control^text_waterfall^main_screen_control"
    case "science":
        widget_list "normal_sci" "science_2d_view^ship_data^text_waterfall^science_data^object_sorted_list"
    case "engineering":
        widget_list "normal_engi" "ship_internal_view^grid_object_list^text_waterfall^eng_heat_controls^eng_power_controls^ship_data"
    case "comms":
        widget_list "normal_comm" "text_waterfall^comms_waterfall^comms_control^comms_face^object_sorted_list^ship_data"
    case "mainscreen":
        widget_list "normal_main" "3dview^ship_data^text_waterfall"
    case _:
        """Pick a Console"""
end_match

await choice:
    + "Head to the bar":
        ->>EnterBar
end_await
# Need this when return from bar
->console_selected

====== player_comms_station =====

await self comms station:
 + "Say Hi":
    have self tell station "Hello"
    delay sim 1s
    have station tell self "Hi back"
end_await
await self comms station:
 + "Hail":
    have station tell self "Ready"
end_await
await self comms station:
 + "Goodbye":
    have self tell station "Goodbye"
    have station tell self "Later"
end_await
->END



======== harvester_patrol  ==========

max_storage = 1500

event damage:
    #log "damage {event.origin_id} {event.parent_id} {event.selected_id}"
    shooter = to_object(event.origin_id)
    <<-POP if shooter is None
    the_target = to_object(event.selected_id)
    <<-POP if target is None
    storing = get_inventory_value(shooter, "storing")
    storage = get_inventory_value(shooter, "storage")
    <<-POP if storage is None or storing is None
    <<-POP if storage >= max_storage
    <<-POP if not has_role(the_target, storing)
    
    taking = get_inventory_value(the_target, "amount")
    amount = 50
    if taking< 50:
        amount= taking
        do set_inventory_value(the_target, "amount", 0)
    else:
        do set_inventory_value(the_target, "amount", taking - 50)
    end_if
    amount = storage+amount
    if amount > max_storage:
        do set_inventory_value(shooter, "storage", max_storage)
    else:
        do set_inventory_value(shooter, "storage", amount)
    end_if
    log "damage mined {shooter.comms_id} {storing} {storage}"
end_event

await => change_harvester_target & set_target
delay 2s
-> harvester_patrol

==== change_harvester_target ======
=> new_orders | storage_is_full | mine_asteroid 
->END

???????? storage_is_full ????
storage = get_inventory_value(self, "storage")
-> FAIL if storage < max_storage
the_target = closest(self, role("Station"))
->FAIL if the_target is None
->FAIL if the_target == old_target
do set_dedicated_link(self,"target", the_target)
do set_inventory_value(self, "target_changed", True)
->END


???????? mine_asteroid ????
the_target = get_dedicated_link(self, "target")
target_changed = get_inventory_value(self, "target_changed")
->FAIL if target_changed
if the_target is not None:
    ->FAIL if the_target and distance_id(to_id(the_target), to_id(self)) > 700
end_if
old_target = the_target
need = get_inventory_value(self, "storing")
the_target = closest(self, role(need))
->FAIL if the_target is None
->FAIL if the_target == old_target
do set_dedicated_link(self,"target", the_target)
do set_inventory_value(self, "target_changed", True)
->END



======== friendly_patrol  ==========

await => change_target & set_target
delay 2s
-> friendly_patrol

==== change_target ======
=> new_orders | enemy_close | target_gone | arrived_friendly | nothing_to_visit
->END

=== set_target === 
# set target and make sure not to shoot friendly
the_target = to_object(get_dedicated_link(self, "target"))
target_changed = get_inventory_value(self, "target_changed")
if the_target and target_changed:
    #log "{the_target.comms_id} != {self.comms_id} {the_target.side != self.side}"
    do target(sim, self, the_target, the_target.side != self.side)
    do set_inventory_value(self, "target_changed", False)
elif the_target is None and target_changed:
    do clear_target(sim, self)
    do set_inventory_value(self, "target_changed", False)
end_if
->END


???? new_orders ???
target_changed = get_inventory_value(self, "target_changed")
->FAIL if target_changed
ordered_target = get_dedicated_link(self, "ordered_target")
->FAIL if ordered_target is None
the_target = get_dedicated_link(self, "target")
#->FAIL if ordered_target == the_target
# Make sure target is a object
the_target = ordered_target
do set_dedicated_link(self,"target", the_target)
do set_dedicated_link(self, "ordered_target", None)
do set_inventory_value(self, "target_changed", True)
->END

???? enemy_close ???
# Maybe do broad test here?
target_changed = get_inventory_value(self, "target_changed")
->FAIL if target_changed
closest_enemy = closest(self, role("raider"), 3000 )
->FAIL if closest_enemy is None
the_target = get_dedicated_link(self,"target")
#->FAIL if the_target == to_id(closest_enemy)
the_target = to_id(closest_enemy)
do set_dedicated_link(self,"target", the_target)
do set_inventory_value(self, "target_changed", True)
->END


???? target_gone ???
#there is no target
the_target = get_dedicated_link(self, "target")
target_changed = get_inventory_value(self, "target_changed")
->FAIL if target_changed
->FAIL if the_target is None
# The target no longer exists
if not object_exists(sim, the_target):
    the_target = None
    do set_dedicated_link(self,"target", the_target)
    do set_inventory_value(self, "target_changed", True)
    ->END
end_if
-> FAIL if distance_id(to_id(the_target), to_id(self)) < 6000
#target out of range
the_target = None
do set_dedicated_link(self,"target", the_target)
do set_inventory_value(self, "target_changed", True)
->END


???????? arrived_friendly ????
the_target = get_dedicated_link(self, "target")
target_changed = get_inventory_value(self, "target_changed")
->FAIL if target_changed
# Object could have been destroyed
if the_target and object_exists(sim, the_target):
    ->FAIL if to_object(the_target).side != self.side
    ->FAIL if the_target and distance_id(to_id(the_target), to_id(self)) > 700
end_if
old_target = the_target
the_target = closest(self, linked_to(self, "Visit"))
->FAIL if the_target is None
->FAIL if the_target == old_target
do unlink(self, "Visit",  old_target)
do set_dedicated_link(self,"target", the_target)
do set_inventory_value(self, "target_changed", True)
->END

?????? nothing_to_visit ???????
# Still stuff to visit
-> FAIL if len(linked_to(self, "Visit")) > 0
do link(self, "Visit", role("Station"))
the_target = closest(self, linked_to(self, "Visit"))
->FAIL if the_target is None
do set_dedicated_link(self,"target", the_target)
do set_inventory_value(self, "target_changed", True)
->END


