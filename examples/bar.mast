shared barkeep = faces.random_torgoth()

shared martinis = 10
shared beer = 10
shared vodka = 8

shared customers = []
shared messages = []
message = ""

my_face = faces.random_terran()

# Listen for disconnect
# Remove the client's face
event disconnect:
~~customers.remove(my_face)~~
refresh GotoBar
end_event

===== EnterBar ===
~~ customers.append(my_face) ~~
refresh GotoBar

============== GotoBar ===================
section style="area: 0,0,60,80-30px;"

face barkeep
"""""""""""""""""""""""""""""""""
Thirsty?
I have
"""""""""""""""""""""""""""""""""
^^^    {martinis} Martini{"s" if martinis>1 else ""}^^^ if martinis>0
^^^    {"and " if martinis>0 and (vodka) == 0 else ""}{beer} beer{"s" if beer>1 else ""}^^^ if beer > 0
^^^    {"and " if (beer+martinis) > 0 else ""}{vodka} vodka^^^ if vodka > 0
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
...
nothing
....
I'm all out. 
If you go to DS1 and get more supply them the Next one is on me.
^^^^^^^^^^^^ if martinis == 0 and vodka == 0 and beer ==0

section style="area: 0,80,100,100-30px;"
face my_face
for c in customers:
    if c != my_face:
    face c
    end_if
next c

section

section style="area: 60,0, 100,80-30px;row-height: 65px;"
for msg in reversed(messages):
    face msg["face"]
    """{msg["msg"]}"""
    row
next c

section style="area: 0,80-50px, 100,80;"
# holes give the next control its space
hole
hole
input message "Make a toast"

button "send":
    to_send = ~~message.strip()~~
    if len(to_send)>0:

~~~ 
messages.append({"face": my_face, "msg": to_send})
if len(messages)>12:
    messages.pop(0)
~~~
    message = ""
        refresh GotoBar
    end_if
    <<-GotoBar<<
end_button


await choice:
+ "Martini" if martinis > 0:
    shared martinis = martinis-1
+ "Beer"  if beer > 0:
    shared beer = beer-1
+ "Vodka" if vodka > 0:
    shared vodka = vodka-1
+ "Had enough"  if (beer+martinis+vodka) > 0:
 ~~ customers.remove(my_face) ~~
    refresh GotoBar
<<-
end_await
if (beer+martinis+vodka) > 0:
    refresh GotoBar
    ->GotoBar
end_if

section style="area: 30, 20, 80, 60;"

face barkeep
row
"""""""""""""""""""""""""""""""""""
BAR is closed until more supplies arrive
they are available at DS1
"""""""""""""""""""""""""""""""""""
await gui timeout 5s
<<-
